mg:=0.4;	% CHANGE THIS TO GET DIFFERENT SIZES OF FONT (font size = 33*mg pt)

mode_setup;
height#:=mg*33pt#;
width#:=mg*33pt#;
outlinePensize#:=mg*33pt#;
strokePensize#:=mg*11pt#;
define_pixels(height,width);
 
% 楕円描画マクロ
pair center, north, south, east, west;
def fillEllipse(expr centerx, centery, rx, ry) = 
    north := (centerx, centery + ry);
    south := (centerx, centery - ry);
    east := (centerx + rx, centery);
    west := (centerx - rx, centery);
    fill north .. east .. south .. west .. cycle;
enddef;

def drawEllipse(expr centerx, centery, rx, ry) = 
    north := (centerx, centery + ry);
    south := (centerx, centery - ry);
    east := (centerx + rx, centery);
    west := (centerx - rx, centery);
	pickup pencircle scaled strokePensize#;
    draw north .. east .. south .. west .. cycle;
enddef;


beginchar(65,width#,height#,0); "Snowman";
%%%ここよりゆきだるま胴体
    pickup pencircle scaled outlinePensize#;
    x1 = 17/30w; 
    x2 =5/30w; x3 = 8/30w; x4 + x4 = x2 + x3;
    x2 + x8 = x3 + x7 = x4 + x6 = x5 + x5 = 32/30w;
    bot y1 = 0; 
    y2 = y8 = 5/18h;
    y3 = 7/15 h; y7 = 13/30 h; y4 = y6 = 7/12h; y5 = 5/6h;
    draw z3 .. z2 .. {right}z1{right} .. z8 .. z7;
    draw z3 .. z4 .. z5 .. z6 .. z7; 
%%%ここよりぼうし
  x9 = 26/30h; x10 = 9/30w; y9 = 20/30h; y10 = 26/30w;
  z11 - z10 = (z9 - z10) rotated 90 scaled 0.07;
  z12 - z11 = (z9 - z10) scaled 0.18;
  z14 - z15 = z13 - z12 = (z11 - z10) scaled 3;
  z14 - z13 = (z9 - z10) scaled 0.7;
  z16 - z9 = z11 - z10;
  fill z9 -- z10 -- z11 -- z12 -- z13 -- z14 -- z15 -- z16 -- cycle;
%%%ここより目・ボタン・雪
    fillEllipse(12/30w, 18/30h, 3/50w, 3/50w);
    fillEllipse(20/30w, 18/30h, 3/50w, 3/50w);
    fillEllipse(16/30w, 10/30h, 2/50w, 2/50w);
    fillEllipse(16/30w, 5/30h, 2/50w, 2/50w);
    drawEllipse(2/50w, 7/30h, 2/50w, 2/50w);
    drawEllipse(2/30w, 19/30h, 2/50w, 2/50w);
    drawEllipse(5/30w, 23/30h, 2/50w, 2/50w);
    drawEllipse(15/60w, 28/30h, 2/50w, 2/50w);
    drawEllipse(28/30w, 18/30h, 2/50w, 2/50w);
    drawEllipse(48/50w, 25/30h, 2/50w, 2/50w);
%%%ここより口
    pickup pencircle scaled outlinePensize#;
    penpos17(0.3pt,60); penpos18(0.3pt,90); penpos19(0.3pt,120);
    x17 + x19 = x18 + x18 = 32/30w; x17 = 13/30w;
    y17 = y19 = 1/2h; y18 = 28/60h;
    draw z17 .. {right}z18{right} .. z19;
    cutoff(z17, 150); cutoff(z19, 30);
%%%以ここより腕
% 右腕
    z20 = (1/30w, 12/30w);
    z21 = (3/30w, 13/30w);
    z22 = (9/60w, 15/30w);
    z23 = (11/60w, 11/30w);
    draw z20 .. z21 .. z22;
    cutoff(z20, 210); cutoff(z22, 60);
    cutdraw z21 .. z23;
% 左腕
    z24 = (27/30w, 29/60w);
    z25 = (84/90w, 25/60w);
    z26 = (59/60w, 11/30w);
    z27 = (26/30w, 34/90w);
    draw z24 .. z25 .. z26;
    cutoff(z24, 110); cutoff(z26, -50);
    cutdraw z25 .. z27;

  labels(range 1 thru 27);
endchar;
shipit; end.